plugins {
    id 'java'
}

apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

group 'io.github.yasiekz'
archivesBaseName = "sql-event-store"
version '1.0.0-SNAPSHOT'

sourceCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.4.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.28.2'
    testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.28.2'

}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Sql event store'
                packaging 'jar'
                description 'Library which integrates SpringData as event store for event sourcing'
                url 'https://github.com/yasiekz/sql-event-store'

                scm {
                    connection 'scm:git:git://github.com/yasiekz/sql-event-store'
                    developerConnection 'scm:git:git@github.com:yasiekz/sql-event-store.git'
                    url 'https://github.com/yasiekz/sql-event-store'
                }

                licenses {
                    license {
                        name 'GNU General Public License v3.0'
                        url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }

                developers {
                    developer {
                        id 'yasiekz'
                        name 'Jan Zieba'
                        email 'yasiekz@gmail.com'
                    }
                }
            }
        }
    }
}